LBL "SCPEREL"  ; out a b for drwg ellipse isoperim P in S steps; S ENTER  P XEQ SCPEREL
RAD            ; Step S  2..100000 in area 0.. 0.636619771 (* P / 4)
ADV            ; require a printer or a terminal emulator ideally pyilper
"ELLIPSE CALC" ; R22-23, R 20-21 in BPEREL, R 00-19 are used in PERELS and 3 and 4
PRA            ; under CC BY SA creative commons 4.0 pascaldagornet at yahoo dot de
"ISOPERIME OF" ; Sep21 2021 First version tested, then put outputs into desmos.com
PRA
PRX
" IN         "
PRA
STO 20         ; P in R 20 (for later); same R 20 like in BPEREL
X<>Y           
0.636619771    ; 2 / PI  
X<>Y
PRX
" STEPS      "
PRA
"------------"
PRA
/
*
4
/
STO 22         ; Step decrement in 22
0.636619771   
RCL 20
*
4
/
STO 23         ; (2 / PI)*P/4   start of calculation
LBL 01
"X then Y"
PRA
PRX            ; output x (a)
RCL 20
XEQ "BPEREL"
PRX            ; output x (b)
RCL 23
R-P
"RADIUS-ANGLE"
PRA
PRX
X<>Y
PI
2
/
X<>Y
-
PRX
"------------"
PRA
RCL 22
ST- 23
RCL 23
X>0?
GOTO 01
DEG
RTN
END

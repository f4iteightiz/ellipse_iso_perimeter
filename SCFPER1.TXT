LBL "SCFPER1" ;calculus of factor F for drwg ellipse isoperim 4 in S steps
             ;F (or Z) is the parameter in cycloid
             ;see https://www.desmos.com/calculator/dprj2gqsry?lang=de
             ;x=r*t−d*sin(t),y=r−d*cos(t)
             ;by using t^Z instead of t¹ in the previous equation, we come
             ; to a good looking solution: Z as constant is good; as variable 
             ; its becoming much better. This program search all possible
             ; values of Z for the interval t 0..1. Based on the results,
             ; it will be possible to curve fit a function F(t)=Z passing
             ; through the t 0..1
             ;F is solved by using a true calculation of perimeter of value 4
             ;
             ;Inputs
             ;  S ENTER XEQ SCFPER1
             ;  Step S  2..100000 in area 0..1 (t of cycloid)
             ;Outputs
             ;  a and b
             ;  with output of approx/straight/interpolation function
             ;  put outputs into desmos.com for visualization
             ;
             ;use R27, R22-23 
             ;  R 20-21 in BPEREL 
             ;  R 00-19 are used in PERELS and 3 and 4 and 6
             ;
             ;require a printer or a terminal emulator ideally pyilper for log
             ;
             ;create raw files with "hp41uc.exe /t=SCFPER1.TXT /r /k"
             ; then upload in PC emulator / virtual drive / HP41 hardware
             ;
             ;under CC BY SA CreativeCommons 4.0 pascaldagornet at yahoo dot de
             ;
             ;change log
             ;2021 10 19 initial
             ;2021 10 21 header update
             ;
RAD        
CF 00    
ADV           ;printer or a terminal emulator ideally pyilper recommended
"CYCLOID CALC" 
PRA            
"ISOPERIME OF" 
PRA            
4             ;fix the perimeter of 4 here
STO 20        ;P in R 20 (for later); same R 20 like in BPEREL
PRX            
" IN         "
PRA
X<>Y           
PRX
" STEPS      "
PRA
"------------"
PRA
0.636619771   ;2 / PI  
X<>Y
/
STO 22        ;Step decrement in 22
2
SQRT
PI
/
CHS
0.5
+
STO 27        ;secure reuse param 1/2 - SQRT(2)/pi
0.636619771   
RCL 20
*
4
/
STO 23        ;(2 / PI)*P/4   start of calculation
LBL 01
  "X & Y"
  PRA
  PRX           ;output x (a)
  RCL 20
  XEQ "BPEREL"
  PRX           ;output x (b)
  STO 24
  RCL 23
  R-P
  "RADIUS & ANGLE"
  PRA
  PRX
  STO 25        ;storage Radius
  X<>Y
  PI
  2
  /
  X<>Y
  -
  PRX
  STO 26        ;storage angle
  0.00000001
  0.99999999
  "YCYCL"
  ASTO 06       ;if using SOL of MATH instead of SOLVE from Advantage
  XEQ "SOLVE"   ;can use SOL of MATH instead
  "CYCLOID t:"
  PRA
  PRX
  STO 28        ;storage t
  1.0000001
  1.2
  "XCYCL"       ;identify Z exponent of cycloid
  ASTO 06   
  XEQ "SOLVE" 
  "Z of CYCLOID:"
  PRA
  PRX
  "------------"
  PRA
  RCL 22
  ST- 23
  RCL 23
  X>0?
GOTO 01
DEG
RTN
LBL "YCYCL"  
PI
*
COS
CHS
1
+
RCL 27
*
CHS
1
+
RCL 25
-
RTN
LBL "XCYCL"  
RCL 28    ; t
X<>Y
Y^X       ; t^z
PI
*
4
/
RCL 28
PI
*
SIN
CHS
RCL 27
*
+
RCL 26
-
RTN
END

LBL "CALPERE" ; calc P based on a b; A ENTER B ENTER XEQ CALPERE
X>Y?          ; date 2021 09 29; R24-26 for no collision with SCPEREL and use in it
X<>Y          ; under CC BY SA creative commons 4.0 pascaldagornet at yahoo dot de
X=0?          ; modified cycloid giving result of error < 0.0256%
GTO 04
STO 07        ; b in 07
X<>Y
STO 08        ; a in 08 and now is a bigger than b
RAD       
2
SQRT
PI
/
CHS
0.5
+
STO 24        ; secure reuse param 1/2 - SQRT(2)/pi
0.000000001
0.999999999
"ZWFT"
XEQ "SOLVE"   ; solve from Advantage
PI
*
COS
CHS
1
+
RCL 24
*
CHS
1
+
1/X
RCL 07
X^2
RCL 08
X^2
+
SQRT
*
4
*
RTN
LBL "ZWFT"   ; calulating the t for the atan(b/a) angle
STO 25       ; secure parameter t
PI
*
SIN
RCL 24
*
CHS
STO 26       ; temporary storage
RCL 25
1.159652180  ; factor electron bohr magneton
Y^X
PI
*
4
/
ST+ 26
RCL 08
RCL 07
/
ATAN
ST+ 26
PI
2
/
ST- 26
RCL 26
RTN
LBL 04
X<>Y
4
*
RTN
END

LBL "SCFPER3" ;scanning b for drwg ellipse isoperim 4 in S steps
             ;b is the parameter in interpolation conic (ellipse)
             ;X^2 +bXY +cY^2 +dX +eY +F =0 for an isoperimeter curve
             ;THIS b IS NOT THE ELLIPSE HALF PARAMETER
             ;This program search all possible
             ; values of b for the interval X Y 0..2n/pi Based on the results,
             ; it will be possible to curve fit a function 
             ; for a true (approximate) calculation of perimeter of value 4
             ;
             ;Inputs
             ;  S ENTER XEQ SCFPER3
             ;  Step S  2..100000 in area 0..2/pi
             ;
             ;Outputs
             ;  X 
             ;  Y 
             ;  Radius 
             ;  Angle    .. all above for ideal isoperimeter
             ;  b        .. for the ideal ellipse fitting curve
             ;  1/b
             ;  X/Y      .. as additional output (X and Y same as above)
             ;  etc. see output example below
             ;
             ;THEN put these outputs into desmos.com for visualization
             ;
             ;typical output
             ;
             ;--START-----
             ;ELLIPSE CALC
             ;ISOPERIME OF
             ; 4.000000000 ***
             ; IN         
             ; 500.0000000 ***
             ; STEPS      
             ;------------
             ;X-Y & R-ANG >
             ; 0.636619771 ***
             ; 0.636619774 ***
             ; 0.900316316 ***
             ; 0.785398162 ***
             ; b
             ; 1.115384618 ***
             ; 1/b
             ; 0.896551722 ***
             ; X/Y
             ; 0.999999996 ***
             ; 1-(X/Y)^2
             ; 0.000000008 ***
             ; SQRT(1-(X/Y)^2
             ; 0.000091652 ***
             ; 1-(X/Y)^4
             ; 0.000000017 ***
             ; SQRT(1-(X/Y)^4
             ; 0.000129615 ***
             ; 1-/+X/Y**2
             ; 0.000000004 ***
             ; SQRT1-/+X/Y**2
             ; 0.000064807 ***
             ; (Y-X)/(Y+X)
             ; 0.000000002 ***
             ;------------
             ;X-Y & R-ANG >
             ; .....
             ; 
             ;use R22-28
             ;  R 20-21 in BPEREL 
             ;  R 00-19 are used in PERELS and 3 and 4 and 6
             ;
             ;require a printer or a terminal emulator ideally pyilper for log
             ;
             ;create raw files with "hp41uc.exe /t=SCFPER3.TXT /r /k"
             ; then upload in PC emulator / virtual drive / HP41 hardware
             ;
             ;under CC BY SA CreativeCommons 4.0 pascaldagornet at yahoo dot de
             ;
             ;change log
             ;2021 10 27 Creation
             ;2021 10 29 Release
             ;2021 11 03 X/Y output added
             ;2021 11 04 SQRT(1+(X/Y)**2) output added
             ;2021 11 08 (1+(X/Y)**2)**-1/2 and SQRT(1-(X/Y)**2) output added
             ;           SQRT(1-(X/Y)**2)/SQRT(1+(X/Y)**2) output added
             ;2021 11 17 (Y-X)/(Y+X) output added
             ;
RAD        
CF 00    
ADV           ;printer or a terminal emulator ideally pyilper recommended
"--START-----"
PRA 
"ELLIPSE CALC" 
PRA            
"ISOPERIME OF" 
PRA            
4             ;fix the perimeter of 4 here
STO 20        ;P in R 20 (for later); same R 20 like in BPEREL
PRX            
" IN         "
PRA
X<>Y           
PRX
" STEPS      "
PRA
0.636619771   ;2 / PI  
X<>Y
/
STO 22        ;Step decrement in 22
0.636619771   
RCL 20
*
4
/
STO 23        ;(2 / PI)*P/4   start of calculation
LBL 01
  "------------"
  PRA
  "X-Y & R-ANG >"
  PRA
  PRX           ;output x (a)   this area will be scanned
  RCL 20
  XEQ "BPEREL"
  PRX           ;output y (b)
  STO 24
  RCL 23
  R-P
  PRX
  X<>Y
  PI
  2
  /
  X<>Y
  -
  PRX
  1.            ;will search the b between 1 and 3.99999 
                ;0.5 is a parabel, so it must be > 0.5
  3.            ;4 is a degenerated ellipse, so it must be <4. 
                ;3 Works
  "BCON"
  ASTO 06       ;if using SOL of MATH instead of SOLVE from Advantage
  XEQ "SOLVE"   ;finding the b of the conic for passing through the isoperimeter
  " b"
  PRA
  PRX     
  " 1/b"
  PRA
  1/X
  PRX     
  ;
  " X/Y"
  PRA
  RCL 23
  RCL 24
  /  
  PRX   
  ;   
  " 1-(X/Y)^2"
  PRA
  X^2
  CHS
  1
  +
  PRX   
  ;   
  " SQRT(1-(X/Y)^2"
  PRA
  SQRT
  PRX  
  ;
  X^2
  RCL 23
  RCL 24
  /  
  X^2
  1
  +
  *
  " 1-(X/Y)^4"
  PRA
  PRX    
  ;
  SQRT
  " SQRT(1-(X/Y)^4"
  PRA
  PRX   
  ; 
  RCL 23
  RCL 24
  /  
  X^2
  CHS
  1
  +      
  RCL 23
  RCL 24
  /  
  X^2
  1
  +
  /
  " 1-/+X/Y**2"
  PRA
  PRX    
  ;
  SQRT
  " SQRT1-/+X/Y**2"
  PRA
  PRX   
  " (Y-X)/(Y+X)"
  PRA
  RCL 24
  RCL 23
  -
  LASTX
  RCL 24
  +
  /
  PRX   
  ;
  RCL 22
  ST- 23
  RCL 23
  X>0?
GOTO 01
DEG
"---ENDE-----"
PRA
RTN
LBL "BCON"  
STO 25          ; for later use of b (from conic equation) as parameter
2
PI
-
X^2
*
12
+
PI
4
*
-
2
PI
-
X^2
/
STO 26          ; for later use of c (not dependent of n) see conic equation
CHS
1
+
4
*
PI
/
RCL 25
-
STO 27          ; for later use: this is e/n
RCL 26
+
CHS
STO 28          ; f/n**2 
;
; lets solve equation of second degree
; first create the C
RCL 24          ;Y of ellipse RCL xx 
X^2
ST* 26
RCL 25
RCL 24
*
RCL 23          ;X of ellipse RCL xx 
*
ST+ 26
RCL 23
X^2
ST+ 26          ; R26 is now C
; create the B
-1
ST* 25
RCL 23
ST* 25
RCL 24
RCL 27
*
ST+ 25
; A is R28
; Mitternacht Formel
RCL 26
RCL 28
*
4
*
CHS
RCL 25
X^2
+
SQRT
RCL 25
+
CHS
RCL 28
/
2
*
RCL 20
-
RTN
END
